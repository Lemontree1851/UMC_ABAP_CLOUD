managed implementation in class zbp_r_tbc1004 unique;
strict ( 2 );
with draft;

define behavior for ZR_TBC1004 alias User
persistent table ztbc_1004
draft table ztbc_1004_d
etag master LocalLastChangedAt
lock master total etag LastChangedAt
authorization master ( instance )
{
  create;
  update;
  delete;

  field ( readonly, numbering : managed ) UserUuid;
  field ( readonly )
  CreatedBy,
  CreatedAt,
  LastChangedBy,
  LastChangedAt,
  LocalLastChangedAt;

  field ( mandatory : create, readonly : update ) UserId;
  field ( mandatory ) Mail;

  validation validationUserId on save ##NOT_ASSIGNED_TO_DETACT { field UserId; create; }
  validation validationEmail on save ##NOT_ASSIGNED_TO_DETACT { field Mail; create; update; }

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare;

  association _AssignPlant { create; with draft; }
  association _AssignCompany { create; with draft; }
  association _AssignSalesOrg { create; with draft; }
  association _AssignRole { create; with draft; }

  mapping for ztbc_1004
    {
      UserUuid           = user_uuid;
      UserId             = user_id;
      Mail               = mail;
      Department         = department;
      UserName           = user_name;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LastChangedBy      = last_changed_by;
      LastChangedAt      = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
    }
}

define behavior for ZR_TBC1006 alias AssignPlant
persistent table ztbc_1006
draft table ztbc_1006_d
lock dependent by _User
authorization dependent by _User
etag master LocalLastChangedAt
{
  update;
  delete;

  field ( readonly, numbering : managed ) Uuid;
  field ( readonly )
  UserUuid,
  PlantName,
  CreatedBy,
  CreatedAt,
  LastChangedBy,
  LastChangedAt,
  LocalLastChangedAt;

  field ( mandatory ) Plant;

  validation validatePlant on save ##NOT_ASSIGNED_TO_DETACT { field Plant; create; update; }

  association _User { with draft; }

  mapping for ztbc_1006
    {
      Uuid               = uuid;
      UserUuid           = user_uuid;
      Plant              = plant;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LastChangedBy      = last_changed_by;
      LastChangedAt      = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
    }
}

define behavior for ZR_TBC1007 alias AssignRole
persistent table ztbc_1007
draft table ztbc_1007_d
lock dependent by _User
authorization dependent by _User
etag master LocalLastChangedAt
{
  update;
  delete;

  field ( readonly, numbering : managed ) Uuid;
  field ( readonly )
  UserUuid,
  RoleUuid,
  RoleName,
  FunctionId,
  AccessId,
  AccessName,
  CreatedBy,
  CreatedAt,
  LastChangedBy,
  LastChangedAt,
  LocalLastChangedAt;

  field ( mandatory ) RoleId;

  validation validateRoleId on save ##NOT_ASSIGNED_TO_DETACT { field RoleId; create; update; }
  determination setRoleUuid on save { field RoleId; }

  association _User { with draft; }

  mapping for ztbc_1007
    {
      Uuid               = uuid;
      UserUuid           = user_uuid;
      RoleUuid           = role_uuid;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LastChangedBy      = last_changed_by;
      LastChangedAt      = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
    }
}

define behavior for ZR_TBC1012 alias AssignCompany
persistent table ztbc_1012
draft table ztbc_1012_d
lock dependent by _User
authorization dependent by _User
etag master LocalLastChangedAt
{
  update;
  delete;

  field ( readonly, numbering : managed ) Uuid;
  field ( readonly )
  UserUuid,
  CompanyCodeName,
  CreatedBy,
  CreatedAt,
  LastChangedBy,
  LastChangedAt,
  LocalLastChangedAt;

  field ( mandatory ) CompanyCode;

  validation validateCompanyCode on save ##NOT_ASSIGNED_TO_DETACT { field CompanyCode; create; update; }

  association _User { with draft; }

  mapping for ztbc_1012
    {
      Uuid               = uuid;
      UserUuid           = user_uuid;
      CompanyCode        = company_code;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LastChangedBy      = last_changed_by;
      LastChangedAt      = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
    }
}

define behavior for ZR_TBC1013 alias AssignSalesOrg
persistent table ztbc_1013
draft table ztbc_1013_d
lock dependent by _User
authorization dependent by _User
etag master LocalLastChangedAt
{
  update;
  delete;

  field ( readonly, numbering : managed ) Uuid;
  field ( readonly )
  UserUuid,
  SalesOrganizationName,
  CreatedBy,
  CreatedAt,
  LastChangedBy,
  LastChangedAt,
  LocalLastChangedAt;

  field ( mandatory ) SalesOrganization;

  validation validateSalesOrg on save ##NOT_ASSIGNED_TO_DETACT { field SalesOrganization; create; update; }

  association _User { with draft; }

  mapping for ztbc_1013
    {
      Uuid               = uuid;
      UserUuid           = user_uuid;
      SalesOrganization  = sales_organization;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LastChangedBy      = last_changed_by;
      LastChangedAt      = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
    }
}